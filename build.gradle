group 'ee.euro2'
version '0.0.2-SNAPSHOT'

buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'signing'
apply plugin: 'maven-publish'

project.version = '0.0.2-SNAPSHOT'
sourceCompatibility = 1.8

jar {
    baseName = 'wallet-server-client'
}

repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/ethereum/maven" }
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
//        mavenLocal()
        maven {
            def repoUrl = project.version.endsWith('-SNAPSHOT') ? "https://oss.sonatype.org/content/repositories/snapshots" : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            url repoUrl
            credentials {
                username "${System.env.OSSRH_USERNAME}"
                password "${System.env.OSSRH_PASSWORD}"
            }
        }
    }
}

dependencies {
    compile('org.springframework:spring-core:4.3.7.RELEASE')
    compile('org.springframework:spring-context:4.3.7.RELEASE')
    compile('org.springframework:spring-web:4.3.7.RELEASE')
    compile('io.springfox:springfox-swagger2:2.6.1')
    compile('io.springfox:springfox-swagger-ui:2.6.1')
    compile('org.projectlombok:lombok:1.16.18')
    compile('org.ethereum:ethereumj-core:1.3.1-RELEASE') {
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }

    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude module: "spock-core"
    }
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4-rc-3")
    testCompile("org.spockframework:spock-spring:1.1-groovy-2.4-rc-3")
    testCompile('cglib:cglib-nodep:3.2.4') // for stubbing of classes
}
